include "global.tbh"

const IOTHUB_INIT_SIGNATURE=&h1FD3
const IOTHUB_TOPIC_METHOD="$iothub/methods/POST/"
const IOTHUB_TOPIC_PROPERTY_DESIRED="$iothub/twin/PATCH/properties/desired/"
const IOTHUB_TOPIC_PROPERTY_REPORTED="$iothub/twin/PATCH/properties/reported/"
const IOTHUB_TOPIC_TWIN="$iothub/twin/"
const IOTHUB_TOPIC_STREAMS="$iothub/streams/POST/"
const IOTHUB_TOPIC_RESULT="$iothub/twin/res/"
const IOTHUB_API_VERSION="?api-version=2019-03-31"
const IOTHUB_MQTT_API_VERSION="?api-version=2018-06-30"
const TOPIC_TIMESTAMP="iothub-creation-time-utc="

dim iothub_property(IOTHUB_PROPERTY_NUM) as struct_iothub_property
dim iothub_command(IOTHUB_COMMAND_NUM) as struct_iothub_command
dim iothub_state as en_iothub_states
dim iothub_interface as pl_sock_interfaces
dim iothub_rid as dword=0
dim device_bound_message_topic as string(64)
dim server_bound_message_topic as string
dim iothub_init_flag as word = 0
dim iothub_num_of_suback as byte
dim iothub_dps_sock as byte = 255
dim iothub_dps_server as string(64)=""
dim iothub_assigned_hub as string(64)=""
dim iothub_scope_id as string(16)
dim iothub_device_id as string(32)
dim iothub_primary_key as string(64)
dim iothub_sas_token as string
dim iothub_timer as byte=0
dim iothub_dps_authorization as string
dim iothub_current_json_key as string
dim iothub_current_json_value as string
dim iothub_http_buf as string
dim iothub_assigned_status as boolean

declare iothub_current_url as string
declare mqtt_sock_num as byte
declare function iothub_init() as ok_ng
declare sub iothub_debugprint(byref print_data as string)
declare function iothub_get_descriptor_field(line_end_pos as word, byref field_start_pos as word) as string
declare function iothub_generate_sas_token(byref resourceUri as string, byref signingKey as string, expiresInMins as word) as string
declare sub iothub_dps_device_provisioning()
declare sub iothub_dps_connect(byref target_ip as string)
declare sub iothub_http_reset()
declare sub iothub_http_request_complete()

public sub iothub_start(byref device_id as string, byref scope_id as string, byref dps_url as string, byref symmetricKey as string, interface as pl_sock_interfaces)
	
	iothub_init_flag=IOTHUB_INIT_SIGNATURE

	if device_id="" or scope_id="" or dps_url="" or symmetricKey="" then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_start() invalid parameters")
		#endif
		exit sub
	end if
	iothub_device_id=device_id
	iothub_scope_id=scope_id
	iothub_dps_server=dps_url
	iothub_interface=interface
	iothub_primary_key=symmetricKey
	server_bound_message_topic="devices/"+iothub_device_id+"/messages/events/"
	device_bound_message_topic="devices/"+iothub_device_id+"/messages/devicebound/"
	iothub_state=EN_IOTHUB_NOT_CONNECTED
	
end sub

function iothub_get_url() as string
	iothub_get_url=iothub_current_url
end function
public sub iothub_on_connected()

	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if

	mqtt_sub(device_bound_message_topic+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(IOTHUB_TOPIC_METHOD+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(IOTHUB_TOPIC_RESULT+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(IOTHUB_TOPIC_PROPERTY_DESIRED+"#",QOS_AT_MOST_ONCE)
	mqtt_sub(IOTHUB_TOPIC_STREAMS+"#",QOS_AT_MOST_ONCE)

	#if IOTHUB_DEBUG_PRINT
		iothub_debugprint("Server connected.")
	#endif
	iothub_state=EN_IOTHUB_CONNECTED
end sub

public sub iothub_on_disconnected()
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	#if IOTHUB_DEBUG_PRINT
		iothub_debugprint("Disconnected from server.")
	#endif
	iothub_state=EN_IOTHUB_NOT_CONNECTED
end sub

public sub iothub_proc_mqtt_msg(byref topic as string,byref data as string)
	dim topic_len as byte=len(IOTHUB_TOPIC_METHOD)
	dim pos1, pos2, pos3 as byte=0
	dim item_name as string
	dim value as string
	dim s as string
	dim rid as string
	dim i as byte
	dim c as byte=0
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if

	if instr(1, topic,IOTHUB_TOPIC_TWIN,1)>0 then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("Properties update: "+data)
		#endif		
		pos1=instr(1,data,"desired",1)
		do 
			pos2=instr(pos1,data,"{",1)
			pos3=instr(pos1,data,"}",1)
			if pos2>0 and pos2<pos3 then
				pos1=pos2+1
				c=c+1
			else
				if pos3>0 then
					c=c-1
					if c=0 then
						s=mid(data,pos1+1,pos3-pos1-1)
					end if
					pos1=pos3+1
				end if
			end if
			if pos2=0 and pos3=0 then
				c=0
			end if			
		loop while c>0

		pos1=1
		do 
			pos2=instr(pos1,s,":",1)
			if pos2>0 then
				item_name=mid(s,pos1,pos2-pos1-1)			
				pos3=instr(pos2,s,",",1)
				if pos3=0 then
					pos3=len(s)+1
				end if
				value=mid(s,pos2+1,pos3-pos2-1)
				dim prev_value as string=stg_get(item_name,0)
				if value<>prev_value then
					#if IOTHUB_DEBUG_PRINT
						iothub_debugprint("Remote value of \x22"+item_name+"\x22 was "+prev_value+" changed to: "+value)
					#endif							
					pos1=instr(1,topic,"$rid=",1)
					if pos1>0 then
						rid=right(topic, len(topic)-pos1-4)
						iothub_rid=lval(rid)
					end if
					'Double confirm setting is set correctly here
					stg_set(item_name,0,value)
					value="{\x22"+item_name+"\x22:"+value+"}"
					iothub_rid=iothub_rid+1
					mqtt_publish(IOTHUB_TOPIC_PROPERTY_REPORTED+"?$rid="+str(iothub_rid),value,QOS_AT_MOST_ONCE)
				end if
			
					pos1=pos3+1
				
			end if
		loop while pos2>0		
	end if
end sub

public sub iothub_command_respond(byref command as string, byref returns as string, result as en_iothub_command_codes)

	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	#if IOTHUB_COMMAND_NUM>0
	dim i as byte
	dim topic as string
	for i=0 to IOTHUB_COMMAND_NUM-1
		if iothub_command(i).name=command then
			if returns="" then
				returns="{}"
			end if
			topic="$iothub/methods/res/"+str(result)+"/?$rid="+str(iothub_command(i).rid)
			mqtt_publish(topic,returns,QOS_AT_MOST_ONCE)
		end if
	next i
	#endif
end sub

public sub iothub_update_telemetry()
	
	
end sub

public sub iothub_proc_timer()

	select case iothub_state
	case EN_IOTHUB_NOT_CONNECTED:
		iothub_device_lookup()			
	case EN_IOTHUB_NOT_PROVISIONED:
		iothub_dps_device_provisioning()
	case EN_IOTHUB_CONNECTED
		iothub_send_telemetry()
	end select	
	
end sub

function iothub_get_descriptor_field(line_end_pos as word, byref field_start_pos as word) as string
'line_end_pos is an INPUT parameter, field_start_pos RETURNS the field position 
	const FIELD_SEPARATOR="\x09"
	dim i as word
	
	do
		if romfile.pointer>=line_end_pos then
			'the field is missing
			iothub_get_descriptor_field=""
			field_start_pos=0
			exit function
		end if
		
		i=romfile.find(romfile.pointer,FIELD_SEPARATOR,1)
		field_start_pos=romfile.pointer
		if i=0 or i>line_end_pos then
			'no next field separator found on this line, so we assume the field goes to the end of the line
			i=line_end_pos
			iothub_get_descriptor_field=romfile.getdata(i-romfile.pointer)
			romfile.pointer=line_end_pos+1
		else
			iothub_get_descriptor_field=romfile.getdata(i-romfile.pointer)
			romfile.pointer=i+1
		end if
	loop while iothub_get_descriptor_field="" 		
end function

public function iothub_update_json(byref original as string, byref attribute as string, byref value as string) as string
	if original="" then
		original="{}"
	end if
	
	while right(original,1)<>"}"
		original=left(original,len(original)-1)
	wend
		
	if original="{}" then
		if left(value,1)="{" and right(value,1)="}" then
			iothub_update_json="{\x22"+attribute+"\x22:"+value+"}"
		else
			iothub_update_json="{\x22"+attribute+"\x22:\x22"+value+"\x22}"
		end if
	else
		if left(value,1)="{" and right(value,1)="}" then
			iothub_update_json=left(original,len(original)-1)+","+"\x22"+attribute+"\x22:"+value+"}"
		else
			iothub_update_json=left(original,len(original)-1)+","+"\x22"+attribute+"\x22:\x22"+value+"\x22}"
		end if
	end if
end function

sub iothub_send_property(prop_name as string, prop_value as string)
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	dim json as string
	json = iothub_update_json(json,prop_name,prop_value)
	iothub_rid=iothub_rid+1
	mqtt_publish(IOTHUB_TOPIC_PROPERTY_REPORTED + "?$rid="+str(iothub_rid),json,QOS_AT_MOST_ONCE)

end sub

public sub iothub_mqtt_sub_ok()
	iothub_num_of_suback=iothub_num_of_suback+1
	if iothub_num_of_suback>4 then
		iothub_rid=iothub_rid+1
		mqtt_publish(IOTHUB_TOPIC_TWIN+"GET/?$rid="+str(iothub_rid),"{}",QOS_AT_MOST_ONCE)
	end if
end sub

public sub iothub_on_ip_otained()

	
end sub

public sub iothub_send_telemetry()

	if iothub_state<>EN_IOTHUB_CONNECTED then
		exit sub
	end if
	
	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	
	dim ts as string(36)
	dim same_ts as boolean = true
	dim json as string

	while same_ts = true
		dim active_rec_num as word			
		tbl_select("Azure","Azure")
		tbl_get_num_records(active_rec_num,NO)
		if active_rec_num>0 then
			if tbl_record_read_active(1)=EN_TBL_STATUS_OK then
				dim record_type, record_value as string(36)
				tbl_field_sg("time",ts,EN_TBL_GET)
				tbl_field_sg("type", record_type,EN_TBL_GET)
				tbl_field_sg("value",record_value ,EN_TBL_GET)	
				tbl_record_delete(1)	
				json=iothub_update_json(json,record_type,record_value)
				dim next_ts as string
				tbl_get_num_records(active_rec_num,NO)
				if active_rec_num>0 then
					if tbl_record_read_active(1)=EN_TBL_STATUS_OK then
						tbl_field_sg("time",next_ts,EN_TBL_GET)
						if ts <> next_ts then
							same_ts = false
						end if
					end if
				else
					same_ts = false
				end if				
			end if
		else
			exit sub
		end if
	wend
	
			
	dim pos as byte
	if ts<>"" then
		pos=instr(1,server_bound_message_topic,TOPIC_TIMESTAMP,1)
		if pos>0 then
				server_bound_message_topic=left(server_bound_message_topic,pos-1)+TOPIC_TIMESTAMP+ts
		else
			server_bound_message_topic=server_bound_message_topic+TOPIC_TIMESTAMP+ts
		end if
	end if
	
	sock.num=mqtt_sock_num
	sock.notifysent(0)
	mqtt_publish(server_bound_message_topic,json,QOS_AT_MOST_ONCE)					
	callback_on_telemetry_sent()

end sub

sub iothub_set_datetime(byref dt_string as string)
	
	'Mon, 08 Feb 2021 03:16:19 GMT
	dim daycounts, mincounts as word
	dim curr_daycounts, curr_mincounts as word
	dim syear,smonth,sdate,shour,smin,ssec as string(4)
	dim b as byte
	
	syear=mid(dt_string,15,2)
	smonth=mid(dt_string,9,3)
	sdate=mid(dt_string,6,2)
	shour=mid(dt_string,18,2)
	smin=mid(dt_string,21,2)
	ssec=mid(dt_string,24,2)
	
	select case smonth
	case "Jan": smonth="01"
	case "Feb": smonth="02"
	case "Mar": smonth="03"
	case "Apr": smonth="04"
	case "May": smonth="05"
	case "Jun": smonth="06"
	case "Jul": smonth="07"
	case "Aug": smonth="08"
	case "Sep": smonth="09"
	case "Oct": smonth="10"
	case "Nov": smonth="11"
	case "Dec": smonth="12"
	case else:
	end select
	daycounts=daycount(val(syear),val(smonth),val(sdate))
	mincounts=mincount(val(shour),val(smin))
	
	rtc.getdata(curr_daycounts,curr_mincounts, b)
	if curr_daycounts<>daycounts or curr_mincounts<>mincounts then
		b=val(ssec) 	
		rtc.setdata(daycounts,mincounts,b)
	end if
		
end sub


sub iothub_get_timestring(byref ts_str as string(20),	
	byref time_daycount as word,
	byref time_mincount as word,
	byref time_second_byte as byte)
	dim time_year_str, time_month_str, time_date_str, time_hour_str, time_minute_str, time_second_str as string(4)

	time_year_str=str(year(time_daycount)+2000)
	time_month_str=str(month(time_daycount))
	time_date_str=str(date(time_daycount))
	time_hour_str=str(hours(time_mincount))
	time_minute_str=str(minutes(time_mincount))
	time_second_str=str(time_second_byte)

	if len(time_hour_str) < 2 then 
		time_hour_str="0"+time_hour_str 
	end if
	if len(time_minute_str) < 2 then 
		time_minute_str="0"+time_minute_str 
	end if
	if len(time_second_str) < 2 then 
		time_second_str="0"+time_second_str 
	end if
	if len(time_date_str) < 2 then 
		time_date_str="0"+time_date_str 
	end if
	if len(time_month_str) < 2 then 
		time_month_str="0"+time_month_str 
	end if
	ts_str=time_year_str+"-"+time_month_str+"-"+time_date_str+"T"+time_hour_str+":"+time_minute_str+":"+time_second_str
	ts_str=ts_str+".000Z"

end sub

public sub iothub_store_telemetry(message_type as string, message_value as string)
	
	dim tbl_result as en_tbl_status_codes	
	dim daycounts, mincounts as word
	dim seconds as byte
	dim ts as string(36)
	rtc.getdata(daycounts,mincounts,seconds)
	iothub_get_timestring(ts,daycounts,mincounts,seconds)
	tbl_select("Azure","Azure")
	tbl_result=tbl_field_sg("time",ts,EN_TBL_SET)
	tbl_result=tbl_field_sg("type",message_type,EN_TBL_SET)
	tbl_result=tbl_field_sg("value",message_value,EN_TBL_SET)
	tbl_result=tbl_record_add("")	
	#if IOTHUB_DEBUG_PRINT
		iothub_debugprint("Telemetry stored. Time: " + ts + " Type: " + message_type + " Value: " + message_value)
	#endif	
end sub

public sub iothub_on_telemetry_sent()
	if sock.num<>mqtt_sock_num then
		exit sub
	end if
	iothub_send_telemetry()
end sub

public sub iothub_dps_connect(byref target_ip as string)

	sock.num=iothub_dps_sock
	sock.tlsdeinit()
	if target_ip<>"" then
		sock.targetip=ddstr(target_ip)
	end if
	sock.targetinterface=iothub_interface
	romfile.open(IOTHUB_DPS_TLS_CER)
	sock.tlsinit(romfile.offset)  'check return state
	sock.rxclear()
	sock.txclear()	
	sock.connect()
	
end sub

sub iothub_device_lookup()

	if iothub_init_flag<>IOTHUB_INIT_SIGNATURE then
		#if IOTHUB_DEBUG_PRINT
			iothub_debugprint("ERROR: iothub_init() wasn't used or failed")
		#endif
		exit sub
	end if
	
	iothub_current_url = "https://" + iothub_dps_server + "/" + iothub_scope_id+"/registrations/"+iothub_device_id+IOTHUB_API_VERSION
	iothub_http_reset()
	http_request(HTTP_POST, iothub_current_url, PL_SOCK_INTERFACE_WLN,"{registrationId: \x22"+iothub_device_id+"\x22}")
	iothub_state=EN_IOTHUB_DEVICE_LOOKUP	

end sub

sub iothub_dps_device_provisioning()
 
	if iothub_state<>EN_IOTHUB_PROVISIONING then
		iothub_state=EN_IOTHUB_PROVISIONING
		iothub_dps_authorization=iothub_generate_sas_token(iothub_scope_id+"%2Fregistrations%2F"+iothub_device_id, iothub_primary_key, 525600)
		iothub_current_url = "https://" + iothub_dps_server + "/" + iothub_scope_id+"/registrations/"+iothub_device_id+"/register"+IOTHUB_API_VERSION
		iothub_http_reset()
		http_request(HTTP_PUT, iothub_current_url, PL_SOCK_INTERFACE_WLN,"{registrationId: \x22"+iothub_device_id+"\x22}")	
	end if
	
end sub

public sub iothub_dns_answer_acquired(return_type as en_dns_return_type, byref return_string as string)
	if return_type=EN_DNS_RET_IP then
		iothub_dps_connect(return_string)
	end if
end sub

sub iot_send_headers()

  if http_request_item.url = iothub_current_url then
        http_send_headers("Content-Type","application/json")
		http_send_headers("Authorization", "SharedAccessSignature sr="+iothub_scope_id+"%2Fregistrations%2F"+iothub_device_id+"&sig="+iothub_dps_authorization)
    end if
	
end sub


sub iothub_mqtt_start()
	
	iothub_state=EN_IOTHUB_CONNECTED
	dim user_name as string=iothub_assigned_hub+"/"+iothub_device_id+"/"+IOTHUB_MQTT_API_VERSION	
	dim resourceUri as string= iothub_assigned_hub+"%2Fdevices%2F"+iothub_device_id
	iothub_sas_token="SharedAccessSignature sr="+iothub_assigned_hub+"%2Fdevices%2F"+iothub_device_id+"&sig="+iothub_generate_sas_token(resourceUri, iothub_primary_key, 525600)
	mqtt_start(PL_SOCK_INTERFACE_WLN,iothub_assigned_hub,8883,user_name,iothub_sas_token,iothub_device_id,true)

end sub

sub iothub_json_callback(byref key as string, byref value as string)
	select case iothub_state
	case EN_IOTHUB_DEVICE_LOOKUP:
		if key = "assignedHub" then
			iothub_assigned_hub=value
			callback_iothub_device_assigned(value)
		end if
		if key = "status" and value = "assigned" then
			iothub_assigned_status=true
		end if
		if key = "errorCode" and value  = "trackingId" then
			iothub_assigned_status=false
		end if
	case EN_IOTHUB_PROVISIONING:
		if key = "status" and value = "assigned" then
			iothub_assigned_status=true
		end if
	end select
end sub

function iothub_parse_http(data as string) as string
	iothub_parse_http = ""
	dim start_pos as byte = 0
	dim end_pos as byte = 0
	dim parsing as boolean = true
	dim segment_length as byte

	
	while parsing = true
		segment_length = 255 - len(iothub_http_buf)
		if segment_length > len(data) then
			segment_length = len(data)
		end if
		iothub_http_buf = iothub_http_buf + mid(data,1,segment_length)
		data = right(data,len(data) - segment_length)
		start_pos = instr(1,iothub_http_buf,"\x22",1)
		end_pos = instr(1,iothub_http_buf,"\x22",2)
		
		if end_pos <> 0 then
			dim item as string = mid(iothub_http_buf,start_pos + 1,end_pos - start_pos - 1)
			if iothub_current_json_key = "" then
				iothub_current_json_key = item
			else if iothub_current_json_value = "" then
				iothub_current_json_value = item
				iothub_json_callback(iothub_current_json_key, iothub_current_json_value)
				iothub_current_json_key = ""
				iothub_current_json_value = ""
			end if
		end if
		if end_pos <> 0 then
			iothub_http_buf = right(iothub_http_buf,len(iothub_http_buf) - end_pos)
		else
			parsing = false
		end if
		
	wend
	
end function

sub iot_header_arrival(header_type as string, header_value as string)

	if header_type = "Date" then
		iothub_set_datetime(header_value)
	end if
	
end sub

sub iothub_http_request_complete()
	
	
	if http_request_item.url = iothub_current_url then
		select case iothub_state		
		case EN_IOTHUB_DEVICE_LOOKUP:
			if iothub_assigned_status=false then
				iothub_state=EN_IOTHUB_NOT_PROVISIONED
			end if
			if iothub_assigned_hub<>"" and iothub_assigned_status<>false then
				iothub_mqtt_start()
			end if	
		case EN_IOTHUB_PROVISIONING:
			iothub_state=EN_IOTHUB_NOT_CONNECTED
		end select
	end if
end sub

sub iot_content_arrival(data as string)
	
	if http_request_item.url = iothub_current_url then
		iothub_parse_http(data)
	end if

end sub

sub iothub_http_reset()
	iothub_http_buf = ""
	iothub_current_json_key = ""
	iothub_current_json_value = ""
	iothub_assigned_status = false
end sub

function iothub_generate_sas_token(byref resourceUri as string, byref signingKey as string, expiresInMins as word) as string
	dim key as string="key"
	dim message as string
	dim result as string=strgen(255,"A")
	dim ch as string(1)
	dim s as string
	dim daycounts,mincounts as word
	dim seconds as byte
	dim expires as dword
	dim i as byte
	
	rtc.getdata(daycounts,mincounts,seconds) 'rtc.getdata returns the elapse time since 2000, we need the elapse time since 1970

	expires=daycounts*24*60+mincounts+16302369+expiresInMins
	expires=expires*60+seconds
	
	message=resourceUri+"\x0A"+lstr(expires)
	base64_decode(signingKey,key)
	sock.tlsinit(0)
	hmac256(key,message,result)
	sock.tlsdeinit()
	

	for i=0 to len(result)/2-1
		ch=chr(val("&h"+mid(result,i*2+1,2)))
		s=s+ch
	next i
	base64_encode(s,len(s),result)
	
	iothub_generate_sas_token=""
	for i=1 to len(result)
		ch=mid(result,i,1)
		if ch="/" then
			iothub_generate_sas_token=iothub_generate_sas_token+"%2F"
		else
			if ch="=" then
				iothub_generate_sas_token=iothub_generate_sas_token+"%3D"
			else
				if ch="+" then
					iothub_generate_sas_token=iothub_generate_sas_token+"%2B"
				else
					iothub_generate_sas_token=iothub_generate_sas_token+ch
				end if
			end if
		end if
	next i
	iothub_generate_sas_token=iothub_generate_sas_token+"&se="+lstr(expires)

end function


#if IOTHUB_DEBUG_PRINT
	sub iothub_debugprint(byref print_data as string)
		sys.debugprint("IOTHUB> "+print_data+"\x0d\x0a")
	end sub
#endif	
