'***********************************************************************************************************
'			Tibbit #53  4 mA to 20mA current loop receiver with 16 bit ADC converter
'***********************************************************************************************************

include "global.tbh"

const TBT53_INIT_SIGNATURE = &h1053
const TBT53_STAMP = "TBT53>"
const TBT53_CR_LF = chr(13)+chr(10)

const TBT53_WRITE_ADDR = &h90
const TBT53_READ_ADDR = &h91

#if TBT53_DEBUG_PRINT = 1
	declare sub tbt53_debug_print(data as string)
#endif

dim i2c_num_tbt53 as byte
dim tbt53_init_flag as word

function tbt53_init(use_ssi as no_yes, pin_clk as byte, pin_data as byte, byref channel as byte) as ok_ng
'Call this function to initialize the Tibbit.
'Use_ssi argument specifies whether you want to communicate with this Tibbit using the SSI channel running in the I2C mode (YES), or
'go for direct I/O manipulation a.k.a. bit-banging (NO).
'Note that there is a limited number of SSI channels so if you set use_ssi=YES this function may return NG, meaning that
'there are no free SSI channels left. This doesn't mean failure. Rather, it means that comms will proceed in bit banging mood.
	dim config, hi_byte,lo_byte as byte
	'dim s as string
	#if TBT53_DEBUG_PRINT = 1
		tbt53_debug_print("+++tbt_53_init()+++")
	#endif

	tbt53_init = OK

	tbt53_init_flag = TBT53_INIT_SIGNATURE
	channel = si2c_register("TBT53", pin_data, pin_clk,use_ssi)

	if channel < 4 then
		ssi.channel = channel
		ssi.enabled = NO
		ssi.baudrate=PL_SSI_BAUD_100kHz
		ssi.clkmap = pin_clk
		ssi.dimap = pin_data
		ssi.domap = pin_data
		ssi.direction = PL_SSI_DIRECTION_LEFT
		ssi.mode = PL_SSI_MODE_2
		ssi.zmode = PL_SSI_ZMODE_ENABLED_ON_ZERO
		ssi.enabled = YES
	else
		if use_ssi = YES then
			tbt53_init = NG
		end if
	end if

	si2c_get(channel)
	si2c_start()
	si2c_write(TBT53_WRITE_ADDR)
	si2c_write(&h0C)
	si2c_stop()

	si2c_start()
	si2c_write(TBT53_READ_ADDR)
	hi_byte = si2c_read(true)
	lo_byte = si2c_read(true)
	config = si2c_read(false)
	si2c_stop()

	#if TBT53_DEBUG_PRINT = 1
		tbt53_debug_print("config : " + hex(config))
		tbt53_debug_print("---tbt_53_init()---")
	#endif
end function

function tbt53_get_current(byref ma as real, channel as byte) as ok_ng
'Performs ADC conversion and returns PREVIOUS conversion result expressed in A.

	dim hi_byte,lo_byte,config as byte
	dim tmp as word
	dim f as no_yes=NO
	dim rtn as real

	const LSB_V = 0.000152587890625	'  5 V / 323768
	const mA_On_V = 0.0032			' (20 mA - 4 mA) / 5000 mV

	tbt53_get_current = NG
	if tbt53_init_flag <> TBT53_INIT_SIGNATURE then exit function

	si2c_get(channel)
	si2c_start()
	si2c_write(TBT53_READ_ADDR)
	hi_byte = si2c_read(true)
	lo_byte = si2c_read(true)
	si2c_stop()

	tmp = hi_byte * 256
	tmp = tmp + lo_byte

	if tmp <= &h7FFF then
		rtn = tmp * LSB_V
	else
		rtn = &hFFFF - tmp + 1
		rtn = -(rtn * LSB_V)
	end if

	#if TBT53_DEBUG_PRINT = 1
		tbt53_debug_print(hex(hi_byte) + "/" + hex(lo_byte) + "/" + hex(config))
		tbt53_debug_print(ftostr(rtn,FTOSTR_MODE_AUTO,5) + " v")
	#endif
	if rtn < -0.6 then exit function

	ma = rtn * mA_On_V

	ma = ma + 0.004

	tbt53_get_current = OK
end function

sub tbt53_th_release(channel as byte)
	if tbt53_init_flag <> TBT53_INIT_SIGNATURE then exit sub
	tbt53_init_flag = &hFFFF
	si2c_release(channel)
end sub

#if TBT53_DEBUG_PRINT=1
sub tbt53_debug_print(data as string)
	sys.debugprint(TBT53_STAMP + data + TBT53_CR_LF)
end sub
#endif
