include "global.tbh"

#define DT_SECS_IN_DAY 86400

const datetime_seconds_since_epoch = 946684800
dim datetime_tz_offset as en_td_timezones 'Time Zone Off Set, You will need to change this according to your time zone
dim datetime_convert_err as ok_ng
dim datetime_format_err as ok_ng
dim datetime_lat as float
dim datetime_lng as float

function datetime_to_timestamp(days as word, mins as word, secs as byte) as dword
	datetime_to_timestamp = datetime_seconds_since_epoch
	datetime_to_timestamp = datetime_to_timestamp + (days * DT_SECS_IN_DAY) + (mins * 60) + secs
end function

function datetime_current_timestamp() as dword
	dim datetime_days,datetime_minutes as word
	dim datetime_secs as byte
	rtc.getdata(datetime_days,datetime_minutes,datetime_secs)
	td_local_to_gmt(datetime_days,datetime_minutes,datetime_tz_offset,PL_OFF)
	datetime_current_timestamp = datetime_seconds_since_epoch
	datetime_current_timestamp = datetime_current_timestamp + (datetime_days * DT_SECS_IN_DAY) + (datetime_minutes * 60) + datetime_secs
end function

sub datetime_from_timestamp(timestamp as dword, byref days as word, byref mins as word, byref secs as byte)
	dim remainder as dword
	days = 0
	mins = 0
	secs = 0
	timestamp = timestamp - datetime_seconds_since_epoch
	remainder = timestamp MOD DT_SECS_IN_DAY
	days = (timestamp - remainder) / DT_SECS_IN_DAY
	timestamp = remainder
	remainder = timestamp MOD 60
	mins = (timestamp - remainder) / 60
	secs = remainder
end sub

sub datetime_time_to_str(byref ts_str as string(14),	
	byref time_daycount as word,
	byref time_mincount as word,
	byref time_second_byte as byte)
	dim time_year_str, time_month_str, time_date_str, time_hour_str, time_minute_str, time_second_str as string(4)

	time_year_str=str(year(time_daycount)+2000)
	time_month_str=str(month(time_daycount))
	time_date_str=str(date(time_daycount))
	time_hour_str=str(hours(time_mincount))
	time_minute_str=str(minutes(time_mincount))
	time_second_str=str(time_second_byte)

	if len(time_hour_str) < 2 then 
		time_hour_str="0"+time_hour_str 
	end if
	if len(time_minute_str) < 2 then 
		time_minute_str="0"+time_minute_str 
	end if
	if len(time_second_str) < 2 then 
		time_second_str="0"+time_second_str 
	end if
	if len(time_date_str) < 2 then 
		time_date_str="0"+time_date_str 
	end if
	if len(time_month_str) < 2 then 
		time_month_str="0"+time_month_str 
	end if
	ts_str=time_year_str+time_month_str+time_date_str+time_hour_str+time_minute_str+time_second_str
end sub

function datetime_timestamp_to_string(timestamp as dword, byref format as string) as string
	dim days,mins as word
	dim secs as byte
	dim result as string=""
	datetime_from_timestamp(timestamp,days,mins,secs)
	td_gmt_to_local(days,mins,datetime_tz_offset,PL_OFF)
	datetime_string_format(result,format,days,mins,secs)
	datetime_timestamp_to_string = result
end function

sub datetime_string_format(byref result as string(14), 
	byref format as string(14),
	byref time_daycount as word,
	byref time_mincount as word,
	byref time_second_byte as byte)
	datetime_time_to_str(result, time_daycount, time_mincount, time_second_byte)

	dim max as byte=len(format)
	dim index as byte=1
	dim dstr as string=""
	while index<max
		dim cur_str as string=mid(format,index,1)
		select case cur_str
		case "Y":
			dstr=dstr+mid(result,1,4)
			index=index+4
		case "M":
			dstr=dstr+mid(result,5,2)
			index=index+2
		case "D":
			dstr=dstr+mid(result,7,2)
			index=index+2
		case "h":
			dstr=dstr+mid(result,9,2)
			index=index+2
		case "m":
			dstr=dstr+mid(result,11,2)
			index=index+2
		case "s":
			dstr=dstr+mid(result,13,2)
			index=index+2
		case else:
			dstr=dstr+cur_str
			index=index+1
		end select
	wend
	result=dstr
end sub

sub datetime_time_to_bytestr(
	byref ts_str as string(14),	
	byref time_daycount as word,
	byref time_mincount as word,
	byref time_second_byte as byte
	)
	ts_str=chr(time_daycount/256)
	ts_str=ts_str+chr(time_daycount and &hFF)
	ts_str=ts_str+chr(time_mincount/256)
	ts_str=ts_str+chr(time_mincount and &hFF)
	ts_str=ts_str+chr(time_second_byte)				
end sub

'------------------------------------------------------------------------------
sub datetime_bytestr_to_time(
	byref ts_str as string(14),	
	byref time_daycount as word,
	byref time_mincount as word,
	byref time_second_byte as byte
	)
	time_daycount=256*asc(mid(ts_str,1,1))+asc(mid(ts_str,2,1))
	time_mincount=256*asc(mid(ts_str,3,1))+asc(mid(ts_str,4,1))
	time_second_byte=asc(mid(ts_str,5,1))
end sub

sub datetime_str_to_time(byref ts_str as string(14),	
	byref time_daycount as word,
	byref time_mincount as word,
	byref time_second_byte as byte)
	dim time_year, time_month,time_date,time_hour,time_min as byte
	dim time_year_str, time_month_str, time_date_str, time_hour_str, time_minute_str, time_second_str as string(4)
	
	time_year_str=left(ts_str,4)
	time_month_str=mid(ts_str,5,2)
	time_date_str=mid(ts_str,7,2)
	time_hour_str=mid(ts_str,9,2)
	time_minute_str=mid(ts_str,11,2)
	time_second_str=mid(ts_str,13,2)
	time_year=val(time_year_str)-2000
	time_month=val(time_month_str)
	time_date=val(time_date_str)
	time_daycount=daycount(time_year,time_month,time_date)
	if time_daycount=65535 then 
		datetime_convert_err=NG
		exit sub
	end if
	time_hour=val(time_hour_str)
	time_min=val(time_minute_str)
	time_mincount=mincount(time_hour,time_min)
	if time_mincount=65535 then
		datetime_convert_err=NG
		exit sub
	end if	
	time_second_byte=val(time_second_str)
	if time_second_byte>59 then datetime_convert_err=NG
end sub

sub datetime_type_convert(
	byref ts_str as string(14),	
	byref time_daycount as word,
	byref time_mincount as word,
	byref time_second_byte as byte, 
	convert_dir as datetime_convert_dir	
	)
'Convert time format, between string(YYYYMMDDhhmmss), and values(daycount, mincout and sec), and value string(values in a 5 bytes asciix string)
'Ex. BYTESTR_TO_TIME: user have to supply Daycount, MinCount, and second, the function returns 5 bytes time value (2bytes of daycounts, 2bytes of mincounts, and 1byte of second in ts_str.)
'TIME_TO_BYTESTR: reverse of BYTESTR_TO_TIME
'STR_TO_TIME: convert a time string in ts_str, to daycounts, mincounts and seconds
'TIME_TO_STR: reverse of STR_TIME
	datetime_convert_err=OK
	select case convert_dir
	case TIME_TO_STR:
		if time_daycount>36524 or time_mincount>1439 or time_second_byte>59 then
			datetime_convert_err=NG
			exit sub
		end if
		datetime_time_to_str(ts_str,time_daycount,time_mincount,time_second_byte)
	case STR_TO_TIME:
		datetime_str_to_time(ts_str,time_daycount,time_mincount,time_second_byte)
	case TIME_TO_BYTESTR:
		if time_daycount>36524 or time_mincount>1439 or time_second_byte>59 then
			datetime_convert_err=NG
			exit sub
		end if	
		datetime_time_to_bytestr(ts_str,time_daycount,time_mincount,time_second_byte)
	case BYTESTR_TO_TIME:
		datetime_bytestr_to_time(ts_str,time_daycount,time_mincount,time_second_byte)
	case STR_TO_BYTESTR:
		datetime_str_to_time(ts_str,time_daycount,time_mincount,time_second_byte)
		if datetime_convert_err=NG then exit sub
		datetime_time_to_bytestr(ts_str,time_daycount,time_mincount,time_second_byte)
	case BYTESTR_TO_STR:
		datetime_bytestr_to_time(ts_str,time_daycount,time_mincount,time_second_byte)
		if time_daycount>36524 or time_mincount>1439 or time_second_byte>59 then
			datetime_convert_err=NG
			exit sub
		end if		
		datetime_time_to_str(ts_str,time_daycount,time_mincount,time_second_byte)
	end select
end sub

function datetime_str_to_timestamp(byref datestr as string, byref format as string) as dword
    datetime_str_to_timestamp=0
    dim param_str as string=datestr
    dim days as word=0
    dim mins as word=0
    dim secs as byte=0
    select case format
        case "YYYYMMDD":
            param_str=param_str+"000000"
        case "YYYYMMDDhhmm":
            param_str=param_str+"00"
        case "YYYYMMDDhhmmss":
            param_str=param_str
        case "hhmmss":
            param_str="20000101"+param_str
        case "hhmm":
            param_str="20000101"+param_str+"00"
    end select

    datetime_type_convert(param_str,days,mins,secs,STR_TO_TIME)
    datetime_str_to_timestamp=datetime_to_timestamp(days,mins,secs)
end function

sub datetime_astro(byref a as astro_struct)
	'36526 is the number of days since 1900/1/1 to 2000/1/1 
	'Excel Date counts 1900 as leap! So number used in noaa calcualtor is 44865
	'this work is based on the NOAA 
	const tpl = 0.0041666667  
	dim days as dword = daycount(a.yy, a.mm, a.dd) + 36526   
	a.jd=days+2415018.5+tpl-a.tz/60/24.0
	a.jc=(a.jd-2451545.0)/36525.0
	a.gmls=(280.46646+a.jc*(36000.76983 + a.jc*0.0003032)) mod 360
	a.gmas=357.52911+a.jc*(35999.05029 - 0.0001537*a.jc)
	a.ecceo=0.016708634-a.jc*(0.000042037+0.0000001267*a.jc)
	a.seqcrt=sin(a.gmas)*(1.914602-a.jc*(0.004817+0.000014*a.jc))+sin(2*a.gmas)*(0.019993-0.000101*a.jc)+sin(3*a.gmas)*0.000289
	a.stl=a.gmls+a.seqcrt
	a.sta=a.gmas+a.seqcrt
	a.sal=a.stl-0.00569-0.00478*sin(125.04-1934.136*a.jc)
	a.moe=23+(26+((21.448-a.jc*(46.815+a.jc*(0.00059-a.jc*0.001813))))/60)/60
	a.oc=a.moe+0.00256*cos(125.04-1934.136*a.jc)
	a.rtasc=atan2(cos(a.oc)*sin(a.sal),cos(a.sal))
	a.sdec=asin(sin(a.oc)*sin(a.sal))
	a.y=tan(a.oc/2)*tan(a.oc/2)
	a.eqtm=4*(a.y*sin(2*a.gmls) - 2*a.ecceo*sin(a.gmas) + 4*a.ecceo*a.y*sin(a.gmas)*cos(2*a.gmls) - 0.5*a.y*a.y*sin(4*a.gmls) - 1.25*a.ecceo*a.ecceo*sin(2*a.gmas))*180.0/3.14159265
	a.hasr=acos(cos(90.833)/(cos(a.lat)*cos(a.sdec))-tan(a.lat)*tan(a.sdec))
	a.solnoon=((720-4*a.lng-a.eqtm+a.tz)/1440)

	'Sunrise/Solar Noon/Sunsetin seconds since midnight
	a.sunrise=(a.solnoon-a.hasr*4/1440)*24*60*60
	a.sunset=(a.solnoon+a.hasr*4/1440)*24*60*60
	a.solnoon=a.solnoon*24*60*60
end sub 

function datetime_next_cron(next_cron as crontab) as dword
	dim timestamp as dword = datetime_current_timestamp()
	dim days as word=0
	dim mins as word=0
	dim secs as byte=0
	dim c_month as byte
	dim c_date as byte
	dim c_hour as word
	dim c_mins as word
	dim c_weekday as byte
	dim tmp as dword=0

	if next_cron.month="" OR next_cron.day="" OR next_cron.hour="" OR next_cron.minute="" OR next_cron.day_of_week="" then
		datetime_next_cron=4294967295
		exit function
	end if
	
	timestamp = timestamp + 60
	dim ended as boolean = false
	dim skip as boolean = false
	while ended<>true
		skip = false
		datetime_from_timestamp(timestamp,days,mins,secs)
		td_gmt_to_local(days,mins,datetime_tz_offset,PL_OFF)
		c_month=month(days)
		c_date=date(days)
		c_hour=hours(mins)
		c_mins=minutes(mins)
		c_weekday=weekday(days)

		if next_cron.month <> 255 AND next_cron.month <> c_month then
			dim diff as dword
			if val(next_cron.month) > c_month then
				diff = daycount(year(days),val(next_cron.month),1) - daycount(year(days),c_month,date(days))
			else 
				diff = daycount(year(days),12,31) - daycount(year(days),c_month,date(days))
				timestamp = timestamp + (86400 - (60*60*c_hour) - (60*c_mins))
			end if
			timestamp = timestamp + (diff * 24 * 60 * 60)
			skip = true
		end if
		
		if next_cron.day <> 255 AND next_cron.day <> c_date AND skip = false then
			skip = true
			timestamp=timestamp + (24*60*60*1)
			timestamp=timestamp - (60*60*c_hour) - (60*c_mins)
		end if
		
		if next_cron.day_of_week <> 255  AND skip = false then
			dim ci as byte=2
			dim ci_day as string(1)=mid(next_cron.day_of_week,1,1)
			dim cron_trans_weekdays as byte(7) = {7,1,2,3,4,5,6}
			dim found as boolean=false
			while len(ci_day) > 0
				if cron_trans_weekdays(val(ci_day))=c_weekday then
					found=true
				end if
				if ci<>0 then
					ci=ci+1
					ci_day=mid(next_cron.day_of_week,ci,1)
					ci = instr(ci,next_cron.day_of_week,",",1)
				else
					ci_day=""
				end if
			wend
			if found<>true then
				skip = true
				timestamp=timestamp + (24*60*60*1)
				timestamp=timestamp - (60*60*c_hour) - (60*c_mins)
			end if
		end if
		
		if next_cron.astro = 255 then 
			
			if next_cron.hour <> 255 AND next_cron.hour <> c_hour AND skip = false then
				skip = true
				timestamp=timestamp + (60*60*1)
				timestamp=timestamp - (60*c_mins)
			end if
			
			if next_cron.minute <> 255 AND next_cron.minute <> c_mins AND skip = false then
				skip = true
				timestamp=timestamp + (60*1)
			end if				
			
		end if
		
		if next_cron.astro <> 255 AND skip = false then
			dim a as astro_struct
			dim found as boolean=false
			dim curr_timestamp as dword
			dim minutes_offset as integer
			td_get_tzone_offset(datetime_tz_offset, minutes_offset)
			timestamp = timestamp - (60*60*c_hour) - (60*c_mins) - secs
			a.dd = c_date
			a.mm = c_month
			a.yy = year(days)
			a.tz = minutes_offset
			a.lat = datetime_lat
			a.lng = datetime_lng
			datetime_astro(a)
			curr_timestamp = datetime_current_timestamp()
			
			if timestamp + a.sunrise > curr_timestamp AND instr(1, next_cron.astro, "r", 1) > 0 then
				tmp = a.sunrise
				timestamp = timestamp + tmp
				found = true
			end if
			
			if timestamp + a.sunset > curr_timestamp AND instr(1, next_cron.astro, "s", 1) > 0 and found <> true then
				tmp = a.sunset
				timestamp = timestamp + tmp
				found = true
			end if
			
			if found <> true then
				timestamp = timestamp + 86400
				skip = true
			end if
		end if
		
		if skip=false then
			timestamp=timestamp-secs
			ended = true
		end if
	wend
	
	datetime_next_cron=timestamp
end function
